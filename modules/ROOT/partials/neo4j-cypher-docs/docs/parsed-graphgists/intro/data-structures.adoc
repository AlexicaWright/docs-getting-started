[[cypherdoc-utilizing-data-structures]]
= Utilizing Data Structures =










Cypher can create and consume more complex data structures out of the box.
As already mentioned you can create literal lists (`[1, 2, 3]`) and maps (`{name: value}`) within a statement.


There are a number of functions that work with lists.
They range from simple ones like `size(list)` that returns the size of a list to `reduce`, which runs an expression against the elements and accumulates the results.


Let's first load a bit of data into the graph.
If you want more details on how the data is loaded, see <<cypher-intro-importing-csv>>.


[source,cypher]
----
LOAD CSV WITH HEADERS FROM "{csv-dir}/intro/movies.csv" AS line
CREATE (m:Movie { id:line.id, title:line.title, released:toInteger(line.year)});
LOAD CSV WITH HEADERS FROM "{csv-dir}/intro/persons.csv" AS line
MERGE (a:Person { id: line.id })
ON CREATE SET a.name = line.name;
LOAD CSV WITH HEADERS FROM "{csv-dir}/intro/roles.csv" AS line
MATCH (m:Movie { id: line.movieId })
MATCH (a:Person { id: line.personId })
CREATE (a)-[:ACTED_IN { roles: [line.role]}]->(m);
LOAD CSV WITH HEADERS FROM "{csv-dir}/intro/movie_actor_roles.csv" AS line FIELDTERMINATOR ";"
MERGE (m:Movie { title: line.title })
ON CREATE SET m.released = toInteger(line.released)
MERGE (a:Person { name: line.actor })
ON CREATE SET a.born = toInteger(line.born)
MERGE (a)-[:ACTED_IN { roles:split(line.characters,",")}]->(m)
----




Now, let's try out data structures.


To begin with, collect the names of the actors per movie, and return two of them:


[source,cypher]
----
MATCH (movie:Movie)<-[:ACTED_IN]-(actor:Person)
RETURN movie.title AS movie, collect(actor.name)[0..2] AS two_of_cast
----




[queryresult]
----
+---------------------------------------------------------------------+
| movie                      | two_of_cast                            |
+---------------------------------------------------------------------+
| "Wall Street"              | ["Michael Douglas","Martin Sheen"]     |
| "The American President"   | ["Martin Sheen","Michael Douglas"]     |
| "The Shawshank Redemption" | ["Morgan Freeman"]                     |
| "Back to the Future"       | ["Christopher Lloyd","Michael J. Fox"] |
+---------------------------------------------------------------------+
4 rows
----


You can also access individual elements or slices of a list quickly with `list[1]` or `list[5..-5]`.
Other functions to access parts of a list are `head(list)`, `tail(list)` and `last(list)`.


== List Predicates


When using lists and arrays in comparisons you can use predicates like `value IN list` or `any(x IN list WHERE x = value)`.
There are list predicates to satisfy conditions for `all`, `any`, `none` and `single` elements.


[source,cypher]
----
MATCH path =(:Person)-->(:Movie)<--(:Person)
WHERE ANY (n IN nodes(path) WHERE n.name = 'Michael Douglas')
RETURN [n IN nodes(path)| coalesce(n.name, n.title)]
----




[queryresult]
----
+-------------------------------------------------------------+
| [n IN nodes(path) | coalesce(n.name, n.title)]              |
+-------------------------------------------------------------+
| ["Michael Douglas","Wall Street","Charlie Sheen"]           |
| ["Martin Sheen","Wall Street","Michael Douglas"]            |
| ["Charlie Sheen","Wall Street","Michael Douglas"]           |
| ["Martin Sheen","The American President","Michael Douglas"] |
| ["Michael Douglas","The American President","Martin Sheen"] |
| ["Michael Douglas","Wall Street","Martin Sheen"]            |
+-------------------------------------------------------------+
6 rows
----


== List Processing


Sometimes you want to process lists to filter, aggregate (`reduce`) or transform their values.
Those transformations can be done within Cypher or in the calling code.
This kind of list-processing can reduce the amount of data handled and returned, so it might make sense to do it within the Cypher statement.


A simple, non-graph example would be:


[source,cypher]
----
WITH range(1, 10) AS numbers
WITH [n IN numbers | n*n] AS squares
WITH [n IN squares WHERE n > 25] AS large_squares
RETURN reduce(a = 0, n IN large_squares | a + n) AS sum_large_squares
----




[queryresult]
----
+-------------------+
| sum_large_squares |
+-------------------+
| 330               |
+-------------------+
1 row
----


In a graph-query you can filter or aggregate collected values instead or work on array properties.
// need to be rephrased somehow


[source,cypher]
----
MATCH (m:Movie)<-[r:ACTED_IN]-(a:Person)
WITH m.title AS movie, collect({ name: a.name, roles: r.roles }) AS cast
RETURN movie,[actor IN cast WHERE actor.name STARTS WITH 'M']
----




[queryresult]
----
+-----------------------------------------------------------------------------------------------------------------------------------------------------------+
| movie                      | [actor IN cast WHERE actor.name STARTS WITH 'M']                                                                             |
+-----------------------------------------------------------------------------------------------------------------------------------------------------------+
| "Wall Street"              | [{roles -> ["Gordon Gekko"], name -> "Michael Douglas"},{roles -> ["Carl Fox"], name -> "Martin Sheen"}]                     |
| "The American President"   | [{roles -> ["President Andrew Shepherd"], name -> "Michael Douglas"},{roles -> ["A.J. MacInerney"], name -> "Martin Sheen"}] |
| "The Shawshank Redemption" | [{roles -> ["Ellis Boyd 'Red' Redding"], name -> "Morgan Freeman"}]                                                          |
| "Back to the Future"       | [{roles -> ["Marty McFly"], name -> "Michael J. Fox"}]                                                                       |
+-----------------------------------------------------------------------------------------------------------------------------------------------------------+
4 rows
----


== Unwind Lists


Sometimes you have collected information into a list, but want to use each element individually as a row.
For instance, you might want to further match patterns in the graph.
Or you passed in a list of values but now want to create or match a node or relationship for each element.
Then you can use the `UNWIND` clause to unroll a list into a sequence of rows again.


For instance, a query to find the top 3 co-actors and then follow their movies and again list the cast for each of those movies:


[source,cypher]
----
MATCH (actor:Person)-[:ACTED_IN]->(movie:Movie)<-[:ACTED_IN]-(colleague:Person)
WHERE actor.name < colleague.name
WITH actor, colleague, count(*) AS frequency, collect(movie) AS movies
ORDER BY frequency DESC LIMIT 3
UNWIND movies AS m
MATCH (m)<-[:ACTED_IN]-(a)
RETURN m.title AS movie, collect(a.name) AS cast
----




[queryresult]
----
+---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------+
| movie                    | cast                                                                                                                                                 |
+---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------+
| "Wall Street"            | ["Michael Douglas","Martin Sheen","Charlie Sheen","Michael Douglas","Martin Sheen","Charlie Sheen","Michael Douglas","Martin Sheen","Charlie Sheen"] |
| "The American President" | ["Martin Sheen","Michael Douglas"]                                                                                                                   |
+---------------------------------------------------------------------------------------------------------------------------------------------------------------------------------+
2 rows
----


ifndef::backend-pdf[]
ifdef::backend-html,backend-html5,backend-xhtml11,backend-deckjs[]
++++
<p class="cypherdoc-console"></p>
++++
endif::[]
endif::[]
ifndef::backend-pdf[]
ifndef::backend-html,backend-html5,backend-xhtml11,backend-deckjs[]
++++
<simpara role="cypherdoc-console"></simpara>
++++
endif::[]
endif::[]
